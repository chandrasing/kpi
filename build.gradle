buildscript {
    ext {
        springBootVersion = '2.0.3.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

allprojects {
    group = 'org.example'
    version = '0.0.1-SNAPSHOT'
}

subprojects {
    ext {
        springCloudVersion = 'Finchley.RELEASE'
    }

    apply plugin: 'maven'
    apply plugin: 'java'
    apply plugin: 'idea'

    if (project.name != 'common') {
        apply plugin: 'org.springframework.boot'
        apply plugin: 'io.spring.dependency-management'
        apply from: rootDir.getPath() + '/gradle/docker.gradle'

        dependencies {
            testCompile('org.springframework.boot:spring-boot-starter-test')
        }

        dependencyManagement {
            imports {
                mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
            }
        }
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    repositories {
        jcenter()
        maven { url "http://repo.maven.apache.org/maven2" }
    }
}
